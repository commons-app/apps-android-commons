SELECT
      (SAMPLE(?location) as ?location)
      ?item
      (SAMPLE(COALESCE(?itemLabelPreferredLanguage, ?itemLabelAnyLanguage)) as ?label)
      (SAMPLE(COALESCE(?itemDescriptionPreferredLanguage, ?itemDescriptionAnyLanguage, "?")) as ?description)
      (SAMPLE(?classId) as ?class)
      (SAMPLE(COALESCE(?classLabelPreferredLanguage, ?classLabelAnyLanguage, "?")) as ?classLabel)
      (SAMPLE(COALESCE(?icon0, ?icon1)) as ?icon)
      ?wikipediaArticle
      ?commonsArticle
      (SAMPLE(?commonsCategory) as ?commonsCategory)
      (SAMPLE(?pic) as ?pic)
    WHERE {
      # Around given location...
      SERVICE wikibase:around {
        ?item wdt:P625 ?location.
        bd:serviceParam wikibase:center "Point(${LONG} ${LAT})"^^geo:wktLiteral.
        bd:serviceParam wikibase:radius "${RAD}" . # Radius in kilometers.
      }

      { ?item p:P1435 ?monument } UNION { ?item p:P2186 ?monument } UNION { ?item p:P1459 ?monument } UNION { ?item p:P1460 ?monument } UNION { ?item p:P1216 ?monument } UNION { ?item p:P709 ?monument } UNION { ?item p:P718 ?monument } UNION { ?item p:P5694 ?monument }

      # Get Commons category (P373)
      OPTIONAL { ?item wdt:P373 ?commonsCategory. }

      # Get (P18)
      OPTIONAL { ?item wdt:P18 ?pic. }

      # Get the label in the preferred language of the user, or any other language if no label is available in that language.
      OPTIONAL {?item rdfs:label ?itemLabelPreferredLanguage. FILTER (lang(?itemLabelPreferredLanguage) = "${LANG}")}
      OPTIONAL {?item rdfs:label ?itemLabelAnyLanguage}

      # Get the description in the preferred language of the user, or any other language if no description is available in that language.
      OPTIONAL {?item schema:description ?itemDescriptionPreferredLanguage. FILTER (lang(?itemDescriptionPreferredLanguage) = "${LANG}")}
      OPTIONAL {?item schema:description ?itemDescriptionAnyLanguage }

      # Get the class label in the preferred language of the user, or any other language if no label is available in that language.
      OPTIONAL {
        ?item p:P31/ps:P31 ?classId.
        OPTIONAL {?classId rdfs:label ?classLabelPreferredLanguage. FILTER (lang(?classLabelPreferredLanguage) = "")}
        OPTIONAL {?classId rdfs:label ?classLabelAnyLanguage}

        OPTIONAL {
            ?wikipediaArticle   schema:about ?item ;
                                schema:isPartOf <https://.wikipedia.org/> .
          }
        OPTIONAL {
            ?wikipediaArticle   schema:about ?item ;
                                schema:isPartOf <https://en.wikipedia.org/> .
            SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
          }

          OPTIONAL {
            ?commonsArticle   schema:about ?item ;
                                schema:isPartOf <https://commons.wikimedia.org/> .
            SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
          }
      }
    }
    GROUP BY ?item ?wikipediaArticle ?commonsArticle